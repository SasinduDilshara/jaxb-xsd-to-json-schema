/*
 * Copyright (c) 2025, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
}

group = project.group
version = project.version

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

dependencies {
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:${jakartaXmlBindVersion}"
    implementation "org.glassfish.jaxb:jaxb-runtime:${jaxbRuntimeVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-jakarta-xmlbind-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.github.victools:jsonschema-generator:${victoolsVersion}"
    implementation "org.glassfish.jaxb:jaxb-xjc:${jaxbXjcVersion}"
}

task generateJaxbClasses {
    def outputDir = file("${buildDir}/generated-sources/jaxb")
    def schemaDir = file("src/main/resources/schema")

    inputs.dir schemaDir
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        fileTree(dir: schemaDir).include('**/*.xsd').each { xsdFile ->
            ant.taskdef(name: 'xjc',
                    classname: 'com.sun.tools.xjc.XJCTask',
                    classpath: configurations.runtimeClasspath.asPath)
            ant.xjc(destdir: outputDir,
                    package: 'com.example.generated',
                    schema: xsdFile) {
                arg(value: '-extension')
                produces(dir: outputDir, includes: '**/*.java')
            }
        }
    }
}

sourceSets.main.java.srcDir "${buildDir}/generated-sources/jaxb"
compileJava.dependsOn generateJaxbClasses